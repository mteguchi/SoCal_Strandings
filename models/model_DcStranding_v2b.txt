# model_DcStranding_v2b.txt
#
# A jags model to estimate the additional mortality along SS from 
# observed strandings. 
#
# In v2, I use negative binomial rather than Poisson (v1).
# v2b sets mu1 and mu as time-dependent

# 2021-09-16
# Tomo Eguchi

# P_SS is the proportion of SS within all SD coast. 
# mu is the natural and anthropogenic mortality combined
# mu1 is the additional mortality for SS beach (not constant over time)
# r = b - mu, where b is the birth rate
# f is carcass recovery rate

model{

	mean_N[1] ~ dnorm(50000, 1/(5000^2))
	N[1] ~ dlnorm(log(mean_N[1]), 1/(sigma_N^2))
	D1[1] ~ dnegbin(p1, (mu_D1[1] * p1)/(1-p1))

	# Used N in the following lines (mu_D1 and mu_D2) 
	# but convergence failures happen.
	# using mean_N seems to take care of that problem.
	mu_D1[1] <- P_SS * mean_N[1] * (mu[1] + mu1[1]) * f
	loglik[1, 1] <- logdensity.negbin(D1[1], p1, (mu_D1[1] * p1)/(1-p1))

	D2[1] ~ dnegbin(p2, (mu_D2[1] * p2)/(1-p2))
	mu_D2[1] <- (1 - P_SS) * mean_N[1] * mu[1] * f
	loglik[1, 2] <- logdensity.negbin(D2[1], p2, (mu_D2[1] * p2)/(1-p2))

	for (t in 2:T){

		mean_N[t] <- mean_N[t-1] * exp(r[t] - mu1[t])
		N[t] ~ dlnorm(log(mean_N[t]), 1/(sigma_N^2))

		D1[t] ~ dnegbin(p1, (mu_D1[t] * p1)/(1-p1))
		mu_D1[t] <- P_SS * mean_N[t] * (mu[t] + mu1[t]) * f
		loglik[t, 1] <- logdensity.negbin(D1[t], p1, (mu_D1[t] * p1)/(1-p1))

		D2[t] ~ dnegbin(p2, (mu_D2[t] * p2)/(1-p2))
		mu_D2[t] <- (1 - P_SS) * mean_N[t] * mu[t] * f
		loglik[t, 2] <- logdensity.negbin(D2[t], p2, (mu_D2[t] * p2)/(1-p2))

		b[t] <- r[t] + mu[t]

	}
	
	# priors:

	for (t in 1:T){
	  mu1[t] ~ dnorm(0, 1)T(0.001,0.999) #dunif(0.001, 0.2)  #dbeta(1, 5)T(0.001,0.1)
	  mu[t] ~ dnorm(0, 1)T(0.001,0.999) #dunif(0, 0.1)
	  r[t] ~ dnorm(0, 1/(0.1^2))
	
	}


	f ~ dbeta(1, 5)   # less bigger values
	p1 ~ dbeta(1,1)
	p2 ~ dbeta(1,1)

	sigma_N ~ dgamma(0.5, 0.01)
	#sigma_N <- 1/sqrt(tau_N)
}
