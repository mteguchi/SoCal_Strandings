# model_DcStranding_v1a.txt
#
# A jags model to estimate the additional mortality along SS from 
# observed strandings. 
#
# v1 uses Poisson for SS and nonSS strandings
# v1a sets mu1 and mu2 as time-independent


# 2021-09-16
# Tomo Eguchi

# P_SS is the proportion of SS within all SD coast. 
# mu is the natural and anthropogenic mortality combined
# mu1 is the additional mortality for SS beach (constant over time)
# r = b - mu, where b is the birth rate
# f is carcass recovery rate


model{

	mean_N[1] ~ dnorm(50000, 1/(5000^2))
	N[1] ~ dlnorm(log(mean_N[1]), 1/(sigma_N^2))
	D1[1] ~ dpois(mu_D1[1])

	# Used N in the following lines (mu_D1 and mu_D2) 
	# but convergence failures happen.
	# using mean_N seems to take care of that problem.
	mu_D1[1] <- P_SS * mean_N[1] * (mu + mu1) * f
	loglik[1, 1] <- logdensity.pois(D1[1], mu_D1[1])

	D2[1] ~ dpois(mu_D2[1])
	mu_D2[1] <- (1 - P_SS) * mean_N[1] * mu * f
	loglik[1, 2] <- logdensity.pois(D2[1], mu_D2[1])

	for (t in 2:T){

		mean_N[t] <- mean_N[t-1] * exp(r - mu - mu1)
		N[t] ~ dlnorm(log(mean_N[t]), 1/(sigma_N^2))

		D1[t] ~ dpois(mu_D1[t])
		mu_D1[t] <- P_SS * mean_N[t] * (mu + mu1) * f
		loglik[t, 1] <- logdensity.pois(D1[t], mu_D1[t])

		D2[t] ~ dpois(mu_D2[t])
		mu_D2[t] <- (1 - P_SS) * mean_N[t] * mu * f
		loglik[t, 2] <- logdensity.pois(D2[t], mu_D2[t])
 
	}
	
	# priors:
	mu ~ dnorm(0, 1)T(0.001,0.999) #dunif(0, 0.1)
	mu1 ~ dnorm(0, 1)T(0.001,0.999) #dunif(0.001, 0.15)  #dbeta(1, 5)
	
	b <- r + mu

	f ~ dbeta(1, 5)   # less bigger values
	r ~ dnorm(0, 1/(0.1^2))

	sigma_N ~ dgamma(0.5, 0.01)
	#sigma_N <- 1/sqrt(tau_N)
}
