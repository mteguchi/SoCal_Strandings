# model_DcStranding_v2.txt
#
# A jags model to estimate the additional mortality along SS from 
# observed strandings. 
#
# In v2, I use negative binomial rather than Poisson (v1).
# v2d uses just one mu1 that either present or absent and time-dependent mu.

# 2021-09-16
# Tomo Eguchi

# P_SS is the proportion of SS within all SD coast. 
# mu is the natural and anthropogenic mortality combined
# mu1 is the additional mortality for SS beach
# mu2 is the additional mortality for non-SS beaches
# r = b - mu, where b is the birth rate
# f is carcass recovery rate

model{

	mean_N[1] ~ dnorm(50000, 1/(5000^2))
	N[1] ~ dlnorm(log(mean_N[1]), 1/(sigma_N^2))

	D1[1] ~ dnegbin(p1, (mu_D1[1] * p1)/(1-p1))
	mu_D1[1] <- ifelse(D1_idx[1] == 1, 
						P_SS * mean_N[1] * (mu[1] + mu1) * f,
						P_SS * mean_N[1] * mu[1] * f)

	loglik[1, 1] <- logdensity.negbin(D1[1], p1, (mu_D1[1] * p1)/(1-p1))

	D2[1] ~ dnegbin(p2, (mu_D2[1] * p2)/(1-p2))
	mu_D2[1] <- (1 - P_SS) * mean_N[1] * mu[1] * f
	loglik[1, 2] <- logdensity.negbin(D2[1], p2, (mu_D2[1] * p2)/(1-p2))

	for (t in 2:T){

		mean_N[t] <- ifelse(D1_idx[t] == 1,
							mean_N[t-1] * exp(r - mu[t] - mu1),
							mean_N[t-1] * exp(r - mu[t]))

		N[t] ~ dlnorm(log(mean_N[t]), 1/(sigma_N^2))

		D1[t] ~ dnegbin(p1, (mu_D1[t] * p1)/(1-p1))
		mu_D1[t] <- ifelse(D1_idx[t] == 1, 
						P_SS * mean_N[t] * (mu[t] + mu1) * f,
						P_SS * mean_N[t] * mu[t] * f)

		loglik[t, 1] <- logdensity.negbin(D1[t], p1, (mu_D1[t] * p1)/(1-p1))

		D2[t] ~ dnegbin(p2, (mu_D2[t] * p2)/(1-p2))
		mu_D2[t] <- (1 - P_SS) * mean_N[t] * mu[t] * f
		loglik[t, 2] <- logdensity.negbin(D2[t], p2, (mu_D2[t] * p2)/(1-p2))
 
	}
	
	# priors:
	for (k in 1:T){
		mu[k] ~ dnorm(0, 1)T(0.001,0.999) #dunif(0.001, 0.3)
	}

	mu1 ~ dnorm(0, 1)T(0.001,0.999) #dbeta(1, 1)T(0.001,0.99) # dunif(0.001, 0.2)  #


	b <- r + mu

	f ~ dbeta(1, 5)   # less bigger values
	r ~ dnorm(0, 1/(0.1^2))
	p1 ~ dbeta(1,1)
	p2 ~ dbeta(1,1)

	sigma_N ~ dgamma(0.5, 0.01)
	#sigma_N <- 1/sqrt(tau_N)
}
